;

;  Exercise the Linear Parallel Bond contact model

;=========================================================================

model new

model large-strain on

model title 'Generating a bonded assembly (Linear Parallel-Bond contact model)'

​

; Set the domain extent.

; excerpt-oiyv-start

model domain extent -10 10

; excerpt-oiyv-end

​

; Modify the default slots of the Contact Model Assignment Table.

; Here we choose the linear contact model (with kn=1e6) for all contact

; types but ball-ball contacts, to which we assign the linearpbond model.

; Note that only the linear part of the linearpbond model is activated, 

; therefore the model will exactly behave as the linear model.

; excerpt-xwsu-start

contact cmat default model linear property kn 5e6

contact cmat default type ball-ball model linearpbond ...

                                          property kn 5e6 proximity 0.1

; excerpt-xwsu-end

​

; Generate a box 

; excerpt-gavv-start

wall generate box -5 5 one-wall

; excerpt-gavv-end

​

; Distribute balls in the box.

; The seed of the pseudo-random number generator is fixed, ensuring that the 

; same initial configuration will be produced any time this file is executed.

; Note also that the box extent specified with the 'ball distribute' command

; is smaller than the box-wall generated above. This is done to avoid balls 

; escaping from the material vessel. Consequently, the porosity specified 

; with the 'ball distribute' command is tuned down, to achieve the desired 

; porosity upon equilibration.

;

; excerpt-fvvo-start

model random 10001

ball distribute porosity 0.2 radius 0.5 0.6 box -4.5 4.5 

; excerpt-fvvo-end

model save 'balls'

​

; Set ball attributes.

; excerpt-unoa-start

ball attribute density 1000.0 damp 0.7

; excerpt-unoa-end

​

; Calm the system.

; Since the "ball distribute" command has been used above, the balls have

; been created without regard to the overlaps. 

; excerpt-tdiw-start

model cycle 2000 calm 50

; excerpt-tdiw-end

​

; Solve the system to a target limit (here the average force ratio).

; To reach a stable configuration faster, density scaling is activated.

; excerpt-vluc-start

model mechanical timestep scale

model solve ratio-average 1e-3

model mechanical timestep auto

model calm

; excerpt-vluc-end

​

; Install a measurement sphere to monitor stresses within 

; the assembly. Also reset ball displacements.

; excerpt-gpnu-start

measure create id 1 radius 5.0

measure history name '1' stress-xx id 1  

measure history name '2' stress-yy id 1  

measure history name '3' stress-zz id 1

; excerpt-gpnu-end

​

; Install parallel bonds - only contacts with linearpbond model are affected.

; excerpt-exdr-start

contact method bond gap 0.1

contact property pb_kn 1e6 pb_ks 1e6 pb_ten 1e6 pb_coh 1e6 pb_fa 30.0

; excerpt-exdr-end

​

; Reset ball displacement and save the initial state.

; excerpt-kien-start

ball attribute displacement multiply 0.0

model save 'initial'

; excerpt-kien-end

​

; Delete the walls and solve to a target average ratio. 

wall delete

model cycle 1

model solve ratio-average 1e-3

model save 'final1'

​

; Repeat the simulation but set the linear stiffness to 0.0.

; Note that the contact forces calculated at the previous timestep are

; already accumulated to the contacting balls - thus the slight perturbation

; from equilibrium.

model restore 'initial'

wall delete

contact property kn 0.0

model cycle 1

model solve ratio-average 1e-3

model save 'final2'

​

; Repeat the simulation but set the linear stiffness to 0.0

; and force a reset of the linear contact forces. All balls are fixed 

; and the system is cycled for 1 step, to reset the linear forces.

; excerpt-stqj-start

model restore 'initial'

wall delete

contact property kn 0.0 lin_force 0.0 0.0 0.0

ball attribute force-contact multiply 0.0 moment-contact multiply 0.0 

model cycle 1

model solve ratio-average 1e-3

model save 'final3'

